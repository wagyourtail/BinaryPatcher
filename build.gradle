plugins {
  id 'net.minecrell.licenser' version '0.3'
  id 'org.ajoberstar.grgit' version '2.3.0'
  id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse'

group = 'net.minecraftforge'
version = gitVersion()
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

def gitVersion() {
    def desc = grgit.describe(longDescr: true).split('-') as List
    def hash = desc.remove(desc.size() - 1)
    def offset = desc.remove(desc.size() - 1)
    def tag = desc.join('-')
    def branch = grgit.branch.current().name    
    return "${tag}.${offset}${t -> if (branch != 'master') t << '-' + branch}"
}

license {
  header project.file('LICENSE-header.txt')
  include 'net/minecraftforge/binarypatcher/**/*.java'
  newLine false
}

jar {
    manifest.attributes('Main-Class': 'net.minecraftforge.binarypatcher.ConsoleTool')
    manifest.attributes('Implementation-Version': project.version)
}
shadowJar {
    classifier 'fatjar'
    manifest.attributes('Main-Class': 'net.minecraftforge.binarypatcher.ConsoleTool')
    manifest.attributes('Implementation-Version': project.version)
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives shadowJar
    archives sourcesJar
}

dependencies {
    compile 'commons-io:commons-io:2.4'
    compile 'com.google.guava:guava:25.1-jre'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'
    compile 'com.github.jponge:lzma-java:1.3'
    compile 'com.nothome:javaxdelta:2.0.1'
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.+")
    testImplementation("org.powermock:powermock-core:2.0.+")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.5.+")
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact shadowJar

            pom {
                name = 'Binary Patcher'
                description = 'Creates and applies binary patches for jar files at the class level.'
                url = 'https://github.com/MinecraftForge/BinaryPatcher'
                scm {
                    url = 'https://github.com/MinecraftForge/BinaryPatcher'
                    connection = 'scm:git:git://github.com/MinecraftForge/BinaryPatcher.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/BinaryPatcher.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/BinaryPatcher/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty('forgeMavenPassword')) {
                credentials {
                    username project.properties.forgeMavenUser
                    password project.properties.forgeMavenPassword
                }
                url 'http://files.minecraftforge.net/maven/manage/upload'
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}
